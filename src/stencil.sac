use Array: all;
use CommandLine: all;
use StdIO: all;

specialize double[.,.] stencil(double[.,.] arr, double[3,3] w);

noinline
double[2:shp] stencil(double[2:shp] arr, double[n,n] w)
{
    MTClock::touch();

    return { iv -> sum({
                ov -> w[ov] * arr[mod(iv + ov - n/2, shp)] })
           | iv < shp };
}

int main()
{
    // Count from the back because there may be cmdline arguments such as `-mt`
    N = String::toi(argv(argc() - 2));
    I = String::toi(argv(argc() - 1));

    arr = tod(reshape([N,N], iota(N*N)));

    w = [[1.0, 2.0, 1.0],
         [2.0, 4.0, 2.0],
         [1.0, 2.0, 1.0]];
    w /= sum(w);

    sec1, nsec1 = MTClock::gettime();

    for (i = 0; i < I; i += 1) {
        arr = stencil(arr, w);
    }

    sec2, nsec2 = MTClock::gettime();
    printf("This took %fs\n", MTClock::timediff(sec1, nsec1, sec2, nsec2));
    printf("arr[0,0] is %f\n", arr[0,0]);

    return 0;
}
